// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP CS21B001ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    // zx
        CS21B001Mux16(a=x, b=false, sel=zx, out=zxout);

        // nx
       	CS21B001Not16(in=zxout, out=notzxout);
        CS21B001Mux16(a=zxout, b=notzxout, sel=nx, out=nxout);
        

        // zy
        CS21B001Mux16(a=y, b=false, sel=zy, out=zyout);

        // ny
        CS21B001Not16(in=zyout, out=notzyout);
        CS21B001Mux16(a=zyout, b=notzyout, sel=ny, out=nyout);

        // f
        CS21B001And16(a=nxout, b=nyout, out=xandy);
        CS21B001Add16(a=nxout, b=nyout, out=xaddy);
        CS21B001Mux16(a=xandy, b= xaddy, sel=f, out=fout);
        
        // no // And final output
        CS21B001Not16(in=fout, out=notfout);
        CS21B001Mux16(a=fout, b=notfout, sel=no, out[0..7]=outzero1, out[8..15]=outzero2, out[15]=outneg, out=out);

        // Other outputs
        // zr
        CS21B001Or8Way(in=outzero1, out=zr1);
        CS21B001Or8Way(in=outzero2, out=zr2);
        CS21B001Or(a=zr1, b=zr2, out=zr3);
        CS21B001Not(in=zr3, out=zr);

        // ng
        CS21B001Mux(a=false, b=true , sel=outneg, out=ng);
}
